plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(rootProject.file("apikey.properties")))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.sabgil.bbuckkugi"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "KAKAO_SDK_KEY", apiKeyProperties['KAKAO_SDK_KEY'])
        buildConfigField("String", "NAVER_API_CLIENTS_ID", apiKeyProperties['NAVER_API_CLIENTS_ID'])
        buildConfigField("String", "NAVER_API_CLIENTS_SECRET", apiKeyProperties['NAVER_API_CLIENTS_SECRET'])

        resValue("string", "KAKAO_SDK_SCHEME", apiKeyProperties['KAKAO_SDK_SCHEME'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "com.google.android.material:material:$materialVirsion"

    // Test
    testImplementation "junit:junit:$jUnitVersion"
    androidTestImplementation "androidx.test:runner:$runnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

    // KTX
    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"

    // LifeCycle components
    implementation "androidx.lifecycle:lifecycle-extensions:$lifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-service:$lifeCycleServiceVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$runtimeKtxVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$viewModelKtxVersion"

    // FCM
    implementation "com.google.firebase:firebase-messaging:$fcmMessageVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // Nearby
    implementation "com.google.android.gms:play-services-nearby:$nearbyVersion"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltCoreVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltCoreVersion"
    implementation "androidx.hilt:hilt-common:$hiltVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hiltVersion"
    kapt "androidx.hilt:hilt-compiler:$hiltVersion"

    // MultiViewType
    implementation "com.sabgil.mutiviewtype:mutiviewtype:$multiViewTypeVersion"

    // Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Kakao login
    implementation "com.kakao.sdk:v2-user-rx:$kakaoSdkVersion"

    // Naver login
    implementation "com.naver.nid:naveridlogin-android-sdk:$naverSdkVersion"

    // Coroutine Rx Utils
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesRxUtilVersion"
}